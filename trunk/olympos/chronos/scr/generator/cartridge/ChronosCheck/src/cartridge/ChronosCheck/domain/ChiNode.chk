import uml;
import Chronos;

extension common::util;
extension common::umlutils;

extension cartridge::ChiCmf::extensions::util;
extension cartridge::ChiCmf::extensions::globals;
extension cartridge::ChiCmf::extensions::naming;

extension cartridge::ChronosCommon::domain::ChiNode;

/**
 * Check the base class
 */
context ChiNode ERROR
	this.name+" does not inherit from '"+RequiredNodeSuperClass()+"', but from "+this.superClass.name :
	let requiredSuperClass = this.getModel().allOwnedElements().typeSelect(ChiNode).select(e|e.qualifiedName == RequiredNodeSuperClass()).first() :
 		!this.isLibraryClass() ? this.isInheritingFrom(requiredSuperClass) : true
;
/**
 * Check the orderby value
 */
context ChiNode ERROR
	this.name+" does not contain an attribute called '"+this.orderby.split(" ").get(0).trim()+"', which should be used for sorting" :
	let orderCol = this.orderby.split(" ").get(0).trim() :
		orderCol == defaultOrderByName() || orderCol == "none" ? true : this.getChiValuesRecursive().name.contains(orderCol)
;
/**
 * Check the initparams
 */
context ChiNode ERROR
	this.name+" references initparams '"+this.initparams+"', which are not defined as configuration class of type ChiSystem" :
	this.getModel().allOwnedElements().typeSelect(ChiSystem).name.toLowerCase().contains(this.initparams.toLowerCase())
;

context uml::Class ERROR
	"Class name may not be NULL or empty: Package " + ((uml::Package) this.owner).name:
	this.name != null && ((String) this.name).length > 0
;
