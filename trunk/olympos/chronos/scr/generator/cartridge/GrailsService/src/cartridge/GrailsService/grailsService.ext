import uml;
import Chronos;

extension common::util;
extension common::nameNormalizer;

extension cartridge::ChronosCommon::usecases::Activity;
extension cartridge::ChronosCommon::usecases::ActivityNode;
extension cartridge::ChronosCommon::usecases::ProductionRuleset;
extension cartridge::ChronosCommon::usecases::DecisionNode;

String asClassName(uml::Activity this):
	this.name.normalizeClassName() + "Service"
;

String asResultType(uml::Activity this):
	this.getLastNode().outgoing.typeSelect(uml::ObjectFlow).target.typeSelect(uml::DataStoreNode).first().getGrailsTypeName()
;

String asTypeOrMap(String typeName):
	!typeName.isNullOrEmpty() && typeName != "def" ? (
		"new " + typeName + "()"
	) : (
		"[:]"
	)
;


String asResultConstructorCall(uml::Activity this):
	this.asResultType().asTypeOrMap()
;

String asRequestType(uml::Activity this):
	let grailsTypeName = this.getFirstNode().incoming.typeSelect(uml::ObjectFlow).source.typeSelect(uml::DataStoreNode).first().getGrailsTypeName():
	
	grailsTypeName != "def" ? (
		grailsTypeName
	) : (
		""
	)
;

String asRequestConstructorCall(uml::Activity this):
	this.asRequestType().asTypeOrMap()
;

String asPackageName(uml::Activity this):
	this.name.normalizePackageName()
;

String asMainMethodName(uml::Activity this):
	this.getInitialNode().name.normalizeMemberName()
;
	
uml::DataStoreNode getInputVariable(uml::Activity this):
	this.getInputVariables().first()
;

uml::DataStoreNode getOutputVariable(uml::Activity this):
	this.getOutputVariables().first()
;

List[uml::ActivityNode] getMethodNodes(uml::Activity this):
	((List[uml::ActivityNode]) {}
		.addAll(this.node.typeSelect(uml::OpaqueAction).select(e|!e.isDecisionPredecessor()))
		.addAll(this.node.typeSelect(uml::DecisionNode))
	)
	.select(e|e.getProductionRuleset() == null || (e.getProductionRuleset().getLastNodes().contains(e) && !e.getProductionRuleset().getRulesetParents().typeSelect(uml::DecisionNode).first().isLoop()))
;

String getGrailsTypeName(uml::DataStoreNode this):
	let name = this.type.name.normalizeClassName():
	
	!name.isNullOrEmpty() ? 
		name
	:
		"def"
;

String asVariableName(uml::DataStoreNode this):
	this.name.normalizeMemberName()
;

String getSourceVariableName(uml::DataStoreNode this):
	//this.hasKeyword(LIST_ELEMENT_KEYWORD()) ? (
		this.asVariableName()
	//) : (
	//	this.incoming.size > 0 ? (
	//		this.incoming.first().source.asResultName() + "." + this.asVariableName()
	//	) : (
	//		"request"
	//	)
	//)
;

String asClassName(uml::ActivityNode this):
	this.name.normalizeClassName()
;

String asResultName(uml::ActivityNode this):
	this.name.asResult()
;

String asMethodName(uml::ActivityNode this):
	this.name.asMethod()
;

String asSelectionName(uml::DecisionNode this):
	this.asClassName() + "Selection"
;

String asContextName(Ouranos::ProductionRuleset this):
	this.name.normalizeClassName() + "Context"
;

String asResult(String this):
	this.normalizeMemberName() + "Result"
;

String asMethod(String this):
	this.normalizeMemberName()
;
	
String asResultName(Ouranos::ProductionRuleset this):
	this.name.asResult()
;

String asMethodName(Ouranos::ProductionRuleset this):
	this.name.asMethod()
;

String asRuleFileName(Ouranos::ProductionRuleset this):
	this.name.normalizeMemberName()
;

String asFullRuleFilePath(Ouranos::ProductionRuleset this):
	this.getActivity().asClassName() + "/" + this.asRuleFileName() + ".irl"
;

cached List[uml::ActivityNode] getAllNextNodes(uml::ActivityNode this, List[uml::ActivityNode] stoppers):
	this.getAllNextNodesInternal({}, stoppers)
;

private List[uml::ActivityNode] getAllNextNodesInternal(uml::ActivityNode this, List[uml::ActivityNode] list, List[uml::ActivityNode] stoppers):
	!stoppers.contains(this) && !list.contains(this) ? (
		list.add(this) ->
		this.getNextNodes().getAllNextNodesInternal(list, stoppers)
	) : (
		Void
	) ->
	
	list
;

String asFlowName(uml::ActivityEdge this):
	!this.name.isNullOrEmpty() ? (
		this.name.normalizeConstantName()
	) : (
		"FLOW_STATE_" + this.source.outgoing.indexOf(this)
	)
;

boolean isAlreadyProcessed(uml::ActivityNode this):
	let result = this.hasKeyword(PROCESSED_KEYWORD()):
	
	!result ? (
		this.addKeyword(PROCESSED_KEYWORD())
	) : (
		Void
	) ->
	
	result
;

List[uml::ActivityNode] getLastNodes(Ouranos::ProductionRuleset this):
	this.getRulesetParents().select(e|e.getNextNode().getProductionRuleset() != this)
;

List[uml::ActivityNode] getFirstNodes(Ouranos::ProductionRuleset this):
	this.getRulesetParents().select(e|e.getPreviousNode().getProductionRuleset() != this)
;

List[Ouranos::ProductionRuleset] getProductionRulesets (uml::Activity this):
	this.ownedBehavior.typeSelect(Ouranos::ProductionRuleset)
;

List[uml::ActivityEdge] getFlowStates(Ouranos::ProductionRuleset this):
	let decision = this.getRulesetParents().typeSelect(uml::DecisionNode).first():
	
	decision != null ? (
		decision.getNextEdges()
	) : (
		{}
	)
;

List[uml::DecisionNode] getDecisionsWithSelection(uml::Activity this):
	this.getMethodNodes().typeSelect(uml::DecisionNode).select(e|e.getProductionRuleset() == null)
;

String asFullPackageName(Ouranos::ProductionRuleset this):
	CONTEXT_PACKAGE() + "." + this.getActivity().asPackageName()
;

String asFullPackageName(uml::DecisionNode this):
	CONTEXT_PACKAGE() + "." + this.activity.asPackageName()
;

boolean isDecisionPredecessor(uml::OpaqueAction this):
	uml::DecisionNode.isInstance(this.getNextNode()) && this.getNextNode().getProductionRuleset() == null
;

uml::Activity getActivity(Ouranos::ProductionRuleset this):
	this.owner
;

String jrulesFileHeader():
	genericFileHeader()
;

uml::ActivityNode getMergeNode(uml::DecisionNode this):
	let allPaths = this.getNextNodes().toSet().toList():
	let result = allPaths.selectFirst(e|e.findMergeNode(allPaths, {this}) != null).findMergeNode(allPaths, {this}):
	
	result.addKeyword(PROCESSED_KEYWORD()) ->
	
	result
;

private uml::ActivityNode findMergeNode(uml::ActivityNode this, List[uml::ActivityNode] allPaths, List[uml::ActivityNode] stoppers):
	let otherPaths = allPaths.reject(this):
	let localStoppers = ((List[uml::ActivityNode]) {}
		.addAll(stoppers)
		.add(this)):
	
	this.getAllNextNodes(stoppers).selectFirst(e|otherPaths.getAllNextNodes(localStoppers).contains(e))
;

uml::ActivityNode unmarkProcessed(uml::ActivityNode this):
	this.removeKeyword(PROCESSED_KEYWORD()) ->
	this
;

String CONTEXT_PACKAGE(): GLOBALVAR contextPackage;
String SERVICE_PACKAGE(): GLOBALVAR servicePackage;

String LIST_ELEMENT_KEYWORD(): "GrailsListElement";
String PROCESSED_KEYWORD(): "GrailsServiceProcessed";

String localDebug(uml::NamedElement this, String msg):
	info(msg + ": " + this.name) ->
	
	""
;
