/*
 * Copyright (c) 2011 The Olympos Development Team.
 * 
 * http://sourceforge.net/projects/olympos/
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html. If redistributing this code,
 * this entire header must remain intact.
 */


import cwm;
import Chronos;
import uml;

extension common::util;
extension common::umlutils;

// TODO: remove all convertToChild for Activities


// convert functions for uml::Class

cwm::Child convertToChild(uml::Class otherClass) :
	let result = new cwm::Child : (
		result.setTargetType(otherClass.metaType.name.split("::").last()) ->
		result.setTargetOid(otherClass.getId())
	) ->
	result
;

cwm::ManyToMany convertToMany(uml::Class other) :
	let result = new cwm::ManyToMany : (
		result.setTargetType(other.metaType.name.split("::").last()) ->
		result.setTargetOid(other.getId())
	) ->
	result
;

cwm::ManyToMany convertToMany(uml::Class other, String relationType) :
	let result = new cwm::ManyToMany : (
		result.setTargetType(other.metaType.name.split("::").last()) ->
		result.setRelationType(relationType) ->
		result.setTargetOid(other.getId())
	) ->
	result
;

cwm::ManyToMany convertToMany(uml::Class other, String relationType, String targetType) :
	let result = new cwm::ManyToMany : (
		result.setTargetType(targetType) ->
		result.setRelationType(relationType) ->
		result.setTargetOid(other.getId())
	) ->
	result
;

cwm::ManyToMany convertToMany(uml::Association this, uml::Class source) :
	let result = new cwm::ManyToMany:
	let ownEnd = this.memberEnd.selectFirst(e|e.type == source):
	let otherEnd = ownEnd.getOtherEnd():
	let isManyToMany = source.isStereotypeApplied(source.getApplicableStereotype("Chronos::ChiManyToMany")):
	
	info("Association convert: " + this.name + " class: " + source.name + " ends: " + this.memberEnd.type) ->
			
	result.setName(this.name) ->

	result.setRelationType(ownEnd.getMoreSpecificAggregationKind(otherEnd).getUwmRelationType()) ->
	//result.setRelationType(otherEnd.aggregation.getUwmRelationType()) ->
	
	result.setTargetType(getTargetType(otherEnd.type.metaType.name.split("::").last())) ->
	result.setTargetRole(getTargetRole(otherEnd.type.metaType.name.split("::").last(), ownEnd.type.metaType.name.split("::").last())) ->
	result.setTargetOid(otherEnd.type.getId()) ->
	
	(
		let realSourceName = isManyToMany ? otherEnd.name : ownEnd.name:
		let realSourceMultiplicity = isManyToMany ? otherEnd.getUwmMultiplicity() : ownEnd.getUwmMultiplicity():
		let realSourceNavigability = isManyToMany ? otherEnd.getUwmNavigability() : ownEnd.getUwmNavigability():
		
		let realTargetName = isManyToMany ? ownEnd.name : otherEnd.name:
		let realTargetMultiplicity = isManyToMany ? ownEnd.getUwmMultiplicity() : otherEnd.getUwmMultiplicity():
		let realTargetNavigability = isManyToMany ? ownEnd.getUwmNavigability() : otherEnd.getUwmNavigability():	

		result.setSourceName(realSourceName) ->
		result.setSourceMultiplicity(realSourceMultiplicity) ->
		result.setSourceNavigability(realSourceNavigability) ->
			
		result.setTargetName(realTargetName) ->
		result.setTargetMultiplicity(realTargetMultiplicity) ->
		result.setTargetNavigability(realTargetNavigability)
	) ->
	
	result
;

uml::AggregationKind getMoreSpecificAggregationKind(uml::Property ownEnd, uml::Property otherEnd):
	ownEnd.aggregation == uml::AggregationKind::composite || otherEnd.aggregation == uml::AggregationKind::composite ?
		uml::AggregationKind::composite
	: (
		ownEnd.aggregation == uml::AggregationKind::shared || otherEnd.aggregation == uml::AggregationKind::shared ?
			uml::AggregationKind::shared
		:
			uml::AggregationKind::none
	)
;

String getUwmRelationType(uml::AggregationKind this):
	switch(this) {
		case uml::AggregationKind::composite:
			"composition"
		
		case uml::AggregationKind::shared:
			"aggregation"
			
		case uml::AggregationKind::none:
			"association"
		
		default:
			null
	}
;

String getTargetType(String this):
	this
;

String getTargetRole(String otherEnd, String ownEnd):
	switch (otherEnd) {
		case "ChiNode": (
			switch(ownEnd) {
				case "ChiNode": "ChiNodeTarget"
				case "ChiManyToMany": "NMChiNodeChiMany2ManyChiNodeEnd"
				default: "UNKNOWN"
			}
		)
		case "ChiManyToMany": "NodeManyToManySource"
		default: otherEnd
	}
;

String getUwmMultiplicity(uml::Property this):
	this.lowerBound() == 0 && this.upperBound() == -1 ? (
		"*"
	) : (
		this.lowerBound() == this.upperBound() ? (
			this.lowerBound().toString()
		) : (
			this.upperBound() == -1 ? (
				this.lowerBound().toString() + "..*"
			) : (
				this.lowerBound().toString() + ".." + this.upperBound()
			)
		)
	)
;

String getUwmNavigability(uml::Property this):
	this.isNavigable() ?
		"Navigable"
	:
		"Non-Navigable"
;

// convert functions for Activity Elements
// TODO: Target Roles are missing -> check if they are needed for import
// TODO: convertToChild functions for Activity Elements should be deleted after complete change to convertToMany


cwm::ManyToMany convertToMany(uml::ActivityFinalNode other) :
	let result = new cwm::ManyToMany : (
		result.setTargetType("ActivityFinal") ->
		result.setTargetRole("ActivityFinal") ->
		result.setTargetOid(other.getId())
	) ->
	result
;

cwm::ManyToMany convertToMany(uml::OpaqueAction other) :
	let result = new cwm::ManyToMany : (
		result.setTargetType("Activity") ->
		result.setTargetRole("AControlFlowTarget") ->
		result.setTargetOid(other.getId())
	) ->
	result
;

cwm::ManyToMany convertToMany(uml::DecisionNode other) :
	let result = new cwm::ManyToMany : (
		result.setTargetType("ActivityDecision") ->
		result.setTargetType("ADControlFlowTarget") ->
		result.setTargetOid(other.getId())
	) ->
	result
;


cwm::ManyToMany convertToMany(uml::SendSignalAction other) :
	let result = new cwm::ManyToMany : (
		result.setTargetType("ActivitySend") ->
		result.setTargetType("ASControlFlowTarget") ->
		result.setTargetOid(other.getId())
	) ->
	result
;

cwm::ManyToMany convertToMany(uml::AcceptEventAction other) :
	let result = new cwm::ManyToMany : (
		result.setTargetType("ActivityReceive") ->
		result.setTargetType("ARControlFlowTarget") ->
		result.setTargetOid(other.getId())
	) ->
	result
;


