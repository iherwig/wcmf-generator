import uml;
import Chronos;

extension org::openarchitectureware::uml2::profile::profiling;
extension org::openarchitectureware::util::stdlib::io;

extension common::util;
extension common::umlutils;

extension common::chronos::naming;
extension common::chronos::globals;

/**
 * Chronos Stereotypes
 */
String STEREOTYPE_CHI_NODE() : "Chronos::ChiNode";
//Not used
//String STEREOTYPE_CHI_VALUE() : "Chronos::ChiValue";
String STEREOTYPE_CHI_MANY_TO_MANY() : "Chronos::ChiManyToMany";
//TODO: Which Type?
String STEREOTYPE_CHI_ASSOCIATION() : "Chronos::ChiAssociation";

/**
 * Check if a given class belongs to the chronos library
 */
boolean isLibraryClass(uml::NamedElement this) : 
	this.getQualifiedName().startsWith("Model::"+RootPackage()+"::"+LibraryPackage())
;

/**
 * Check if a given class belongs to the application
 */
boolean isApplicationClass(uml::NamedElement this) : 
	this.getQualifiedName().startsWith("Model::"+RootPackage()+"::"+ApplicationPackage()) && this.metaType == "uml::Class"
;

/**
 * Check if an element is added by the generator
 */
boolean isGeneratorAdded(uml::Element this) : 
	JAVA net.sourceforge.olympos.oaw.extend.Generator.isGeneratorAdded(org.eclipse.uml2.uml.Element)
;

/**
 * Check if an element is inherited by the generator
 */
boolean isGeneratorInherited(uml::Element this) : 
	JAVA net.sourceforge.olympos.oaw.extend.Generator.isGeneratorInherited(org.eclipse.uml2.uml.Element)
;

/**
 * Check if an element is added by the generator
 */
boolean isNode(uml::Element this) : 
	this.getAppliedStereotype(STEREOTYPE_CHI_NODE()) != null || this.getAppliedStereotype(STEREOTYPE_CHI_MANY_TO_MANY()) != null
;

/**
 * Get the foreign key
 */
String getFKName(Property parent) :
	let association = parent.association :
	association != null ? 
		(let fk_name = association.getTaggedValue(STEREOTYPE_CHI_ASSOCIATION(), "fk_name") :
			fk_name == null || fk_name.toString().length == 0 ? defaultFKName(parent.type) : fk_name) : 
		defaultFKName(parent.type) 
;

/**
 * Configuration related methods
 */
 
/**
 * Check if a model contains a application configuration
 */
boolean hasSystemConfig(uml::Model this) :
	this.allOwnedElements().typeSelect(ChiSystem).size > 0
;
/**
 * Get all configuration file names that are referenced in the model
 */
Set[String] getConfigurationFiles(uml::Model this) :
	let fromKeys = this.allOwnedElements().typeSelect(ChiActionKey).config :
		let fromSystem = this.allOwnedElements().typeSelect(ChiSystem).config :
			fromKeys.addAll(fromSystem).toSet()
;
/**
 * Check if a configuation file is the default one
 */
boolean isDefaultConfig(String config) : config == DefaultConfigFile();
