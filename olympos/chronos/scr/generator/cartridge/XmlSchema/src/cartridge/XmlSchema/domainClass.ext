import uml;
import xmlSchema;

extension org::openarchitectureware::xsd::lib::qname;

extension common::util;
extension common::umlutils;

extension cartridge::XmlSchema::common;

xmlSchema::TopLevelComplexType convertToXsd(uml::Class this, String targetNamespace, List[String] rootElements):
	//(new xmlSchema::TopLevelComplexType).sequence.e
	//(new xmlSchema::ComplexContentType).exten

	let result = new xmlSchema::TopLevelComplexType:
	let sequence = new xmlSchema::ExplicitGroup:
	let allProperties = this.getAllProperties():
	let parentProperties = this.getSuperClass().getAllProperties():
	
	info("Converting Class " + this.name) ->
	
	allProperties.removeAll(allProperties.select(e|
		(!e.name.isNullOrEmpty() && parentProperties.exists(f|e.name == f.name)) ||
		(e.association != null && parentProperties.exists(f|e.type == f.type))
	)) ->
	
	result.setName(this.name) ->
	result.setAbstract(this.isAbstract) ->
	
	//allProperties.exists(e|e.class == this) ? (
		sequence.element.addAll(allProperties.convertProperty(targetNamespace, rootElements))
	//) : (
	//	Void
	//) ->
	->
	
	this.superClass.size > 0 && this.superClass.forAll(e|!e.isAbstract) ? (
		let complex = new xmlSchema::ComplexContentType:
		let ext = new xmlSchema::ExtensionType:
		
		ext.setBase(createQName(targetNamespace, this.superClass.first().name)) ->
		ext.setSequence(sequence) ->
		
		complex.setExtension(ext) ->
		
		result.setComplexContent(complex)
	) : (
		result.setSequence(sequence)
	) -> 
	
	result
;


xmlSchema::LocalElement convertProperty(uml::Property this, String targetNamespace, List[String] rootElements):
	//(new xmlSchema::LocalElement).t
	
	let result = new xmlSchema::LocalElement:
	
	info("	Converting Property " + this.name) ->
	
	//result.setMinOccurs(this.getLower()) ->
	result.setMinOccurs(0) ->
	result.setMaxOccurs(this.getUpper() != -1 ? this.getUpper() : parseToQName("unbounded")) ->
	result.setName(this.nameOrType()) ->
	result.setType(((this.association == null) || (rootElements.contains(this.class.name))) ? (
		this.name != "mRID" ? (
			this.type.sanitizeXsdTypes(targetNamespace)
		) : (
			parseToQName("schema:ID")
		)
	) : (
		parseToQName("schema:IDREF")
	)) ->
	
	result
;

xmlSchema::QName sanitizeXsdTypes(uml::Type this, String targetNamespace):
	let result = switch(this.name.toLowerCase()) {
		case "string": "schema:string"
		case "integer": "schema:int"
		case "long": "schema:long"
		case "boolean": "schema:boolean"
		case "bool": "schema:boolean"
		case "float": "schema:float"
		case "decimal": "schema:decimal"
		case null: "schema:anyType"
		default: null
	}:
	
	result == null ? (
		createQName(targetNamespace, this.name.toFirstLower())
	) : (
		parseToQName(result)
	)
;

String nameOrType(uml::Property this):
	!this.name.isNullOrEmpty() ? (
		this.name
	) : (
		this.type.name.toFirstLower()
	)
;

List[uml::Property] getAllProperties(uml::Class this):
	let result = (List[uml::Property]) {}:
	
	//info("Class:" + this.name) ->
	//info("ownAssociationEnds: " + this.getOwnAssociationEnds().type) ->

	!this.isAbstract ? (
		let superProperties = (List[uml::Property]) {}:
		let thisProperties = (List[uml::Property]) {}:
		this.getSuperClass().isAbstract ? (
			superProperties.addAll(this.getSuperClass().getOwnAssociationEnds()) ->
			superProperties.addAll(this.getSuperClass().attribute)
		) : (
			Void
		) ->
		result.addAll(superProperties.sortBy(e|e.nameOrType())) ->
		
		thisProperties.addAll(this.attribute) ->
		//info("filtered: " + this.getOwnAssociationEnds().removeDuplicates(result).type) ->
		thisProperties.addAll(this.getOwnAssociationEnds()) ->
		
		result.removeDuplicates(thisProperties) ->
		result.addAll(thisProperties.sortBy(e|e.nameOrType()))
	) : (
		Void
	) ->
	
	//info("result: " + result.type) ->
	//info("result filtered: " + result.toSet().select(e|e.isNavigable()).toList().type) ->
	result.unique().select(e|((uml::Property) e).isNavigable())
;

List[uml::Property] removeDuplicates(List[uml::Property] this, List[uml::Property] list):
	this.select(e|e.name.isNullOrEmpty() || !list.exists(f|e.name == f.name))
;

List unique(List this):
	JAVA org.sourceforge.olympos.xmlschema.ListHelper.unique(java.util.List)
;
